buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:${licensePluginVersion}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayPluginVersion}"
        classpath "com.github.jengelman.gradle.plugins:shadow:${shadowPluginVersion}"
    }
}

repositories {
    jcenter()
}

subprojects {
    version appVersion

    apply plugin: 'maven-publish'
    apply plugin: "com.github.hierynomus.license"
    apply plugin: "com.jfrog.bintray"
    apply plugin: "java"
    apply plugin: 'groovy'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
    }

    license {
        header = file("${rootDir}/LICENSE-SHORT.txt")

        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'Silvio Wangler'
        ext.email = 'silvio.wangler@gmail.com'

        excludes(["**/*.csv"])
    }

    dependencies {

        compile "org.apache.logging.log4j:log4j-api:${log4j2Version}"

        testCompile ("org.spockframework:spock-core:${spockVersion}") {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
        testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
    }
}
